/********************************************************************************
 * Copyright (c) 2020 Annette Pohl
 *
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License v. 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * This Source Code may also be made available under the following Secondary
 * Licenses when the conditions for such availability set forth in the Eclipse
 * Public License v. 2.0 are satisfied: GNU General Public License, version 2
 * with the GNU Classpath Exception which is available at
 * https://www.gnu.org/software/classpath/license.html.
 *
 * SPDX-License-Identifier: EPL-2.0 OR GPL-2.0 WITH Classpath-exception-2.0
 ********************************************************************************/
 

grammar de.acegen.AceGen with org.eclipse.xtext.common.Terminals

generate aceGen "http://www.acegen.de/AceGen"

Project: {Project}
	('HttpClient' httpClient = HttpClient ? | 'HttpServer' httpServer = HttpServer ?)?
;

HttpClient: 
	(react16_8 ?= 'React_16.8')? 
	name = QualifiedName 
	('ACE' (aceOperations += HttpClientAce)*)?
	('ui' container = SingleClientAttribute)?
	('scenarios' (scenarios += ClientScenario)*)?
;

HttpClientAce: 
	(async ?= 'async')? name = ID  
		('(' input += Input (',' input += Input)* ')' )?  
		('fromAppState' '(' refs += FromAppStateRef (',' refs += FromAppStateRef)* ')' )?  
	('call' serverCall = [HttpServerAce | QualifiedName] )? 
	('loadingFlag' loadingFlag = [SingleClientAttribute | QualifiedName])? 
	(outcomes += HttpClientOutcome)*
;

FromAppStateRef:
	stateElement = [SingleClientAttribute | QualifiedName] ('as' varName = ID)?
;

Input:
	name = ID
;

HttpClientOutcome:
	'on' name = ID 
	('(' listeners += HttpClientStateFunction* ')')? 
	('triggers' '(' (aceOperations += [HttpClientAce | QualifiedName])* ')')? 
;

HttpClientStateFunction:
	stateFunctionType = HttpClientStateFunctionType stateElement = [SingleClientAttribute | QualifiedName]
;

HttpClientStateFunctionType:
	'set' | 'merge'
;

ClientAttribute:
	SingleClientAttribute | 'group' GroupedClientAttribute
;

SingleClientAttribute:
	list ?= 'List'? 
	name = ID 
	hash ?= 'location.hash'? 
	storage ?= 'storage'? 
	passOn ?= 'passOn'? 
	('{'
		attributes += ClientAttribute*
	'}')?
;

GroupedClientAttribute:
	name = ID '{' attributeGroup += ClientAttribute ( attributeGroup += ClientAttribute* ) '}' 
;

ClientScenario:
	name = ID
	('GIVEN' (givenRefs += ClientGivenRef)* )? 
	'WHEN' whenBlock = ClientWhenBlock
	'THEN' thenBlock = ClientThenBlock
;

ClientGivenRef:
	scenario = [ClientScenario | QualifiedName]
;

ClientWhenBlock: 
	action = [HttpClientAce|QualifiedName] 
	('(' (inputValues += InputValue (',' inputValues += InputValue)* )? ')')?
	('nonDeterministicValues' nonDeterministicValues += NonDeterministicValue (',' nonDeterministicValues += NonDeterministicValue)*)?
;

NonDeterministicValue:
	{NonDeterministicValue} '{'
		('uuid' uuid = STRING 
		('clientSystemTime' clientSystemTime = STRING)?
		('serverSystemTime' serverSystemTime = STRING)?
		('nonDeterministic' attribute = [Attribute | QualifiedName] ':' value = PrimitiveValue)?)?
	'}'
;

InputValue:
	input = [Input | QualifiedName] ':' value = PrimitiveValue
;

ClientThenBlock: {ClientThenBlock} 
	('expectedState' stateVerifications += StateVerification*)?
	('verifications' verifications += ID*)?
;

StateVerification:
	name = ID stateRef = [SingleClientAttribute | QualifiedName] 'shouldBe' value = JsonValueClient
;

JsonObjectClient:
	{JsonObjectClient} '{' 
		(members+=JsonMemberClient)?
		(',' members+=JsonMemberClient)* 
	
	'}';

JsonMemberClient:
	attribute=[SingleClientAttribute | QualifiedName] ':' value=JsonValueClient;
	
JsonValueClient:
	JsonObjectClient | StringType | JsonArrayClient | BooleanType | NullType | LongType | JsonDateTime | UndefinedType;

JsonArrayClient:
	{JsonArrayClient} '[' (values+=JsonValueClient)? (',' values+=JsonValueClient)* ']';






HttpServer:
	language = ('Java')
	type = ('Dropwizard')?
	persistenceLayer = 'JDBI3'?
	migrations = 'Liquibase' ?
	name = QualifiedName
	('Authorization' authUser = AuthUser)?
	('import' authUserRef = [AuthUser | QualifiedName])?
	('ACE' (aceOperations += HttpServerAce)*	)?
	('views' (views += HttpServerView)* )?
	('models' (models += Model)*)?
	('scenarios' (scenarios += Scenario)*)?
;

HttpServerAce: 
	HttpServerAceWrite | HttpServerAceRead
;

HttpServerAceWrite: 
	name = ID '<' model = [ Model | QualifiedName ] '>' 
	type = WriteFunctionType
	url = STRING
	authorize ?= 'authorize'? 
	('pathParams' (pathParams += AttributeParamRef)*)?
	('queryParams' (queryParams += AttributeParamRef)*)?
	('payload' (payload += AttributeParamRef)*)?
	('response' (response += [Attribute | QualifiedName])*)?
	(outcomes += HttpServerOutcome)*
;

HttpServerOutcome:
	'on' name = ID 
	('('(listeners += [HttpServerViewFunction | QualifiedName])*')')* 
;

HttpServerAceRead: 
	name = ID '<' model = [ Model | QualifiedName ] '>' 
	type = ReadFunctionType
	url = STRING
	authorize ?= 'authorize'? 
	('pathParams' (pathParams += AttributeParamRef)*)?
	('queryParams' (queryParams += AttributeParamRef)*)?
	('payload' (payload += AttributeParamRef)*)?
	('response' (response += [Attribute | QualifiedName])*)?
;

AttributeParamRef:
	notNull ?= 'NotNull'? attribute = [Attribute | QualifiedName] 
;

HttpServerView: 
	afterCommit ?= 'afterCommit'? name = ID 
	('{'
		(renderFunctions += HttpServerViewFunction)*
	'}')?		
;

HttpServerViewFunction:
	name = ID '(' model = [ Model | QualifiedName ] ')'
;

AuthUser: 
	name = ID
	 ('{'
		attributes += Attribute*
	'}')
;

Model:
	persistent ?= 'persistent'? name = ID ('extends' superModels+=[Model|QualifiedName] (',' superModels+=[Model|QualifiedName])* )? ('{'
		attributes += Attribute*
	'}')?
;

Scenario:
	name = ID
	('GIVEN' (givenItems += Given)* )? 
	'WHEN' whenBlock = WhenBlock
	'THEN' thenBlock = ThenBlock
;

Given:
	GivenRef | CustomCall
;

CustomCall:
	customCallName = ID '(' (values += PrimitiveValue (',' values += PrimitiveValue)*)? ')' (times = INT 'x')?
;

GivenRef:
	scenario = [Scenario | QualifiedName] (times = INT 'x')? (excludeGiven ?= 'excludeGIVEN')?
;

WhenBlock: 
	action = [HttpServerAce|QualifiedName] 
	dataDefinition = DataDefinition
	(authorization = Authorization)?
	extractions += Extraction*
;

ThenBlock:
	statusCode = INT 
	('response' 'shouldBe' response = DataDefinition)?
	('persistence' persistenceVerifications += PersistenceVerification*)?
	('verifications' verifications += Verification*)?
;

PersistenceVerification:
	name = ID model = [Model | QualifiedName] expression = PersistenceVerificationExpression
;

PersistenceVerificationExpression:
	SelectByUniqueAttribute | SelectByPrimaryKeys | Count
;

SelectByPrimaryKeys: 
	'selectByPrimaryKey' '(' attributeAndValues += AttributeAndValue (',' attributeAndValues += AttributeAndValue*)? ')' 'shouldBe' expected = SelectByExpectation
;

SelectByUniqueAttribute: 
	'selectBy' '(' attributeAndValue = AttributeAndValue ')' 'shouldBe' expected = SelectByExpectation
;

Count: 
	'filterAndCountBy' '(' attributeAndValues += AttributeAndValue (',' attributeAndValues += AttributeAndValue*)? ')' 'shouldBe' expected = INT
;

AttributeAndValue:
	attribute = [Attribute|QualifiedName] ':' value = PrimitiveValue
;

Verification:
	name = ID
;

Extraction:
	'extract' name = ID  
;

SelectByExpectation:
	object = JsonObject | isNotNull ?= 'notNull' | isNull ?= 'null'	
;

DataDefinition: 
	{DataDefinition} 
	('uuid' uuid = STRING)? 
	('systemTime' systemtime = STRING pattern = STRING)? 
	(data = JsonObject)?
;

Authorization:
	'authorization' username = STRING ':' password = STRING
;

Attribute:
	unique ?= 'Unique'? 
	primaryKey ?= 'PrimaryKey'? 
	notNull ?= 'NotNull'? 
	list ?= 'List'? 
	(type = Type | model = [Model | QualifiedName])
	name = ID 
	('references' foreignKey = [Attribute | QualifiedName])? 
	nonDeterministic ?= 'nonDeterministic'?
;

QualifiedName:
  ID ('.' ID)*
;

WriteFunctionType:
	'POST' | 'PUT' | 'DELETE'
;

ReadFunctionType:
	'GET'
;

Type:
	'Integer' | 'String' | 'Float' | 'Boolean' | 'DateTime' | 'Long'
;

JsonObject: JsonObjectAce | 'json' StringType;
	
JsonObjectAce: 
	{JsonObjectAce} '{' 
		(members+=JsonMember)?
		(',' members+=JsonMember)* 
	
	'}';

JsonMember:
	attribute=[Attribute | QualifiedName] ':' value=JsonValue;
	
JsonValue:
	JsonObject | StringType | JsonArray | BooleanType | NullType | LongType | JsonDateTime;

JsonArray:
	{JsonArray} '[' (values+=JsonValue)? (',' values+=JsonValue)* ']';
	
JsonDateTime:
	dateTime = STRING pattern = STRING
;

StringType:
	string = STRING
;

BooleanType:
	boolean = JsonBoolean
;

NullType:
	null ?= 'null'
;

UndefinedType:
	undefined ?= 'undefined'
;

LongType:
	long = INT
;

JsonBoolean:
  'true' | 'false';

PrimitiveValue:
	string = STRING | long = INT
;

/******* S.D.G. *******/
	
	

